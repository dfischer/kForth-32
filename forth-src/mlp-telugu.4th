\ mlp-telugu.4th
\
\ తెలుగులో ప్రోగ్రామింగ్ చెయ్యి 
\ 
\ మైనేని కృష్ణ
\
\  మార్పులు :
\  13 మే  2017  -- తయారు చేసిన
\  28 నవంబరు 2019

: వెంటనే    immediate ;
: తరువాత  postpone postpone ; వెంటనే 

: ఉంటే 	తరువాత   if     ; వెంటనే 
: కాదంటే 	తరువాత  else   ; వెంటనే   
: అప్పుడు 	తరువాత  then   ; వెంటనే 
: మెుదలు 	తరువాత  begin  ; వెంటనే 
: అయితే 	తరువాత  while  ; వెంటనే 
: ఇంకొసారి తరువాత  repeat ; వెంటనే 
: మళ్ళీ 	తరువాత  again  ; వెంటనే 
: వరకు 	తరువాత  until  ; వెంటనే 
: చెయ్యి 	తరువాత  do     ; వెంటనే 
: తిరుగు 	తరువాత  loop   ; వెంటనే 
: ఐ 		తరువాత  I      ; వెంటనే 
: ఙే 		తరువాత  J      ; వెంటనే 
: దిగు 	తరువాత  leave  ; వెంటనే 
: పారేయి     drop ;
: డూప్       dup ;
: 2డూప్     2dup ;
: మార్చు     swap ;
: గిల్లు         nip ;
: ఎక్కువ     > ;
: తక్కువ    <  ;
: సమానం  =  ;
: సంఖ్య    constant ;
: మారేది    variable ;
: బై   bye ;

\ CONSTANTs "sunna" and "padi"
 0 సంఖ్య సున్న
10 సంఖ్య పది

\ VARIABLE "vI"
మారేది  వి
16 వి !

\ max and min -- IF ... ELSE ... THEN 
: పెద్దది ( n1 n2 -- n1|n2 ) 2డూప్ ఎక్కువ ఉంటే పారేయి కాదంటే గిల్లు అప్పుడు ;
: చిన్నది ( n1 n2 -- n1|n2 ) 2డూప్ తక్కువ ఉంటే పారేయి కాదంటే గిల్లు అప్పుడు ;

\ to-ten ( BEGIN ... UNTIL )
: పది-వరకు ( -- ) సున్న మెుదలు 1+ డూప్ . డూప్ పది సమానం వరకు పారేయి ;

\ ten-times ( DO ... LOOP )
: పది-సారిలు  ( -- ) 10 0 చెయ్యి ఐ . తిరుగు ;

